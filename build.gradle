plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'dev.spiti.utilities'
version '1.0.0'

sourceCompatibility = 1.8

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '3.16'
    implementation group: 'com.opencsv', name: 'opencsv', version: '4.1'
    implementation group: 'log4j', name: 'log4j', version: '1.2.17'
    testImplementation group: 'org.testng', name: 'testng', version: '6.14.3'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'data-reader'
            from components.java
            artifact sourcesJar
            //artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Data Reader'
                description = 'A utility to read test data from excel and csv and write data as needed'
                url = 'https://github.com/spiti-dev/data-reader'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name  = 'Naresh Gourishetty'
                        email = 'naresh.g@spititech.com'
                    }
                    developer {
                        name = 'spiti.dev'
                        email = 'contact@spiti.dev'
                    }
                }
                scm {
                    url = 'https://github.com/spiti-dev/data-reader'
                    connection = 'scm:git:git://github.com/spiti-dev/data-reader.git'
                    developerConnection = 'scm:git:ssh://git@github.com:spiti-dev/data-reader.git'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}